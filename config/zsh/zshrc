#!/usr/bin/env zsh

# time debugging
if [ -z "$_STARTUP_DEBUG_TIME" ]; then
  _STARTUP_DEBUG_TIME=$(date +"%s%3N")
fi

ZSH_RC_PATH="${ZDOTDIR:-$HOME}/.zshrc"
XDG_CACHE_HOME=${XDG_CACHE_HOME:-$HOME/.cache}
XDG_CONFIG_HOME=${XDG_CONFIG_HOME:-$HOME/.config}

exists() { command -v "$1" &>/dev/null; }
sourceIf() {
  [ $# != 1 ] && return 1
  [ -f "$1" ] && source "$1"
}
setTitle() { printf '\033]0;%s\007' "$1"; }
isAndroid() { test "$(uname -o)" = Android; }
isRoot() { [ "$EUID" = 0 ] }

eval "$(lua "$DOTFILES/lib/z.lua" --init zsh enhanced once echo fzf)" || {
  printf >&2 "error: failed to load z.lua\n"
}

# Load some other libraries
sourceIf "$XDG_CONFIG_HOME/dots/env.sh"
sourceIf ~/storage/share/zshrc.personal

# general options
setopt interactivecomments # can use '#' for comments in interactive
setopt histignorespace # entries that start with a ' ' don't get to the history file
setopt PROMPT_SUBST # $(cmd) literals are run on $PS1/$PS2/$RPROMPT

# history config
HISTFILE="$XDG_CACHE_HOME/zhistory"
HISTSIZE=SAVEHIST=100000
setopt sharehistory
setopt extendedhistory
setopt nobanghist # i fuc king hate !!!!!!!
setopt HIST_IGNORE_DUPS

# color plugin config
autoload -U colors && colors

# "edit in editor" plugin config
autoload -z edit-command-line
zle -N edit-command-line
bindkey "^X^E" edit-command-line

# completion plugin config
autoload -U compinit # completion support
zstyle ':completion:*' menu select
zmodload zsh/complist
compinit
_comp_options+=(globdots) # include hidden files in autocomplete

precmd() {
  # get current working directory
  local cwd="$(pwd)"
  if grep -q '^'"$HOME" <<<"$cwd"; then
    cwd="$(sed 's/^'"$(sed 's|/|\\/|g' <<<"$HOME")"'/~/g' <<<"$cwd")"
  fi

  # set terminal title
  setTitle "$(whoami)@${HOST}:$cwd"
}

# construct prompt string
_rootSymbol=$(isRoot && printf '#' || printf '$')
_ps1Symbol="%F{13}Î»: " #  was the old one
isAndroid && _ps1Home="%F{13}... " || _ps1Home='%F{13}%n%F{4}@%F{13}%m '
[ "$SHELL_STACK" ] && _shellStackSym=" %F{4}stack:%F{9}$(printf "%q" "$SHELL_STACK")" || _shellStackSym=""
PS1="
%F{3}[ $_ps1Home%F{14}%~%(?.. %F{4}exit:%F{10}%?)$_shellStackSym %F{3}]
$_ps1Symbol%b%F{5}"

PS2="%F{13} : %{$reset_color%}"
# ! isAndroid && RPROMPT='%F{11}${SHELL_STACK}'
# ! isAndroid && RPROMPT='%F{11}$(git-branch)'
# [ "${NNNLVL:-0}" -ge 1 ] && PS1="%B%F{3}(%F{5}n%F{3})%b $PS1"

# use vim keys in tab completion menu
bindkey -M menuselect 'h' vi-backward-char
bindkey -M menuselect 'k' vi-up-line-or-history
bindkey -M menuselect 'l' vi-forward-char
bindkey -M menuselect 'j' vi-down-line-or-history
bindkey -v '^?' backward-delete-char

# change cursor shape for different vi modes
function zle-keymap-select {
  if [ "$KEYMAP" = vicmd ] || [ "$1" = "block" ]; then
    printf '\033[1 q'
  elif [ "$KEYMAP" = main ] || [ "$KEYMAP" = viins ] \
         || [ "$KEYMAP" = '' ] || [ "$1" = 'beam' ]; then
    printf '\033[5 q'
  fi
}

function zle-line-init {
  zle -K viins # initiate `vi insert` as keymap (can be removed if `bindkey -V` has been set elsewhere)
}

zle -N zle-keymap-select
zle -N zle-line-init

fzhistoryGen() {
  { [ -n "$ZSH_NAME" ] && fc -l 1 || history 1; } \
    | sed -r 's/ *[0-9]*\*? *//' \
    | tac | fzagnostic \
    | sed -r 's \\ \\\\ g'
}

fzhistory() {
  local result=$(fzhistoryGen) || return 1
  print -z "$result"
}

fzSelectFileGen() {
  local item
  fd -tf | fzf -m "$@" | while read item; do
    echo -n "${(q)item} "
  done
}

fzSelectFile() {
  LBUFFER="${LBUFFER}$(fzSelectFileGen)"
  local ret="$?"
  zle reset-prompt
  return "$ret"
}

bindkey -s '^R' '\eS fzhistory\n'
if [ "$FILEMAN" = "lf" ] && exists lf; then
  bindkey -s '^o' '\eS lfAndCd\n'
elif exists nnn; then
  bindkey -s '^o' '\eS nnnAndCd\n'
fi

bindkey -s '^g' '\eS zGoto\n'
bindkey -s '^P' '\e[C'

bindkey '^F' fzSelectFile
zle -N fzSelectFile

lfAndCd() {
  local tmp=$(mktemp)

  (cd "${1:-.}" && lf -last-dir-path="$tmp" "$@")

  if [ -f "$tmp" ]; then
    dir=$(cat "$tmp")
    rm -f "$tmp" >/dev/null
    [ -d "$dir" ] && [ "$dir" != "$(pwd)" ] && cd "$dir"
  fi

  return 0
}

nnnAndCd() {
  # block nesting of nnn in subshells
  if [ -n "$NNNLVL" ] && [ "${NNNLVL:-0}" -ge 1 ]; then
    printf "%s\n" "nnn is already running"
    return 1
  fi

  export NNN_TMPFILE="$XDG_CONFIG_HOME/nnn/.lastd"
  nnn "$@"
  if [ -f "$NNN_TMPFILE" ]; then
    . "$NNN_TMPFILE"
    rm -f "$NNN_TMPFILE" >/dev/null
  fi

  return 0
}

findFileGen() {
  printf "%s (%s)\n" "@updir" ".."
  printf "%s (%s)\n" "@here " "$(pwd)"

  local args=()
  [ "$FINDFILE_SHOWHIDDEN" ] && args+=('-H')
  [ "$FINDFILE_DIRONLY" ] && args+=('-td')

  fd "${args[@]}"
}

findFile() {
  local prog choice choices show_hidden

  if [ "$1" = "-H" ]; then
    show_hidden=1
    shift
  fi

  [ $# = 0 ] && prog="${EDITOR:-vi}" || prog="$1"
  choice="$(FINDFILE_SHOWHIDDEN="$show_hidden" findFileGen | fzagnostic)" || return 1

  case "$choice" in
    "@updir"*) cd .. && findFile ;; # FIXME: annoying recursion
    "@here "*) ;;
    *)
      if [ -f "$choice" ]; then
        "$prog" "$choice"
      elif [ -d "$choice" ]; then
        # FIXME: annoying recursion
        cd "$choice" && findFile
      else
        printf >&2 "Don't know what to do with choice %s; returning" "$choice"
        exit 1
      fi
      ;;
  esac
}

dup-term-gui() {
  [ "$WAYLAND_DISPLAY" ] || [ "$DISPLAY" ] || exit 2
  "$TERMINAL" &>/dev/null & disown
}
dup-term-tmux() {
  [ "$TMUX" ] || exit 2
  tmux new-window -c "$PWD"
}
dup-term-auto() {
  if [ "$WAYLAND_DISPLAY" ] || [ "$DISPLAY" ]; then
    dup-term-gui
  elif [ "$TMUX" ]; then
    dup-term-tmux
  else
    exit 2
  fi
}

Dgui() { dup-term-gui; }
Dtmux() { dup-term-tmux; }
D() { dup-term-auto; }

cdNearFile() {
  [ $# = 1 ] || exit 1

  if [ -f "$1" ]; then
    cd "$(dirname "$1")"
  elif [ -d "$1" ]; then
    cd "$1"
  else
    printf >&2 "%s: invalid file\n" "$0"
  fi
}

pushShell() {
  if [ "$SHELL_STACK" ]; then
    SHELL_STACK="$SHELL_STACK.${1:-zsh}" zsh
  else
    SHELL_STACK="${1:-zsh}" zsh
  fi
}

reloadRc() { source "$ZSH_RC_PATH" }
alias rl='reloadRc'

# Blocked commands
_warnBlockedCmd() {
  printf >&2 "[Command Blocked: %s]\n" "$1"
  return 1
}
r() { _warnBlockedCmd "$0"; }

zGoto() { z -I -t . "$@"; }

alias w='zGoto'
alias f='findFile'
alias fof='findFile openfork'
alias fcd='FINDFILE_DIRONLY=1 findFile cd-to-file'

make() {
  # a *very* crappy workaround for using `make -f`.
  # FIXME: make this less painful somehow
  if [ $# -gt 0 ] && [ "$1" = "-f" ]; then
    printf >&2 '[Warning] `-f` argument detected, abandoning use of `fparent-run`\n'
    command make "$@"
  else
    fparent-run Makefile run-command file-dir make "$@"
  fi
}

alias ls='ls --color=auto'
alias l='ls -l'
alias la='ls -A'
alias du='du -shc'

alias p3='python3'
alias pip='python -m pip'
alias jl='julia'
alias g='git-check'
alias e='$EDITOR'
alias jpdo='LANG=ja_JP.UTF8 LC_ALL=ja_JP'

alias sy='dotsync'
alias of='openfork'

# startXServerOrLogout() { start-xserver || logout; }
# alias sx='startXServerOrLogout'

# sg() { dbus-launch --exit-with-session river-start; }
sg() { wl-start Hyprland; }

editRecentf() { filehist select "$EDITOR"; }
alias er='editRecentf'

if exists nvim; then
  alias vi='nvim' vim='nvim'
elif exists vim; then
  alias vi='vim'
fi

alias grep='grep --color=auto'
alias fgrep='fgrep --color=auto'
alias egrep='egrep --color=auto'

alias ..='go-up'
go-up() {
  for i in {1.."${1:-1}"}; do
    cd ..
  done
}

tmux-nest() {
  TMUX= tmux "$@"
}

# poor man's clifm
# cd() {
#   builtin cd "$@" \
#     && printf "\n" \
#     && ls -F \
#     && printf "------------------------------\n"
# }

# A wrapper for the `info` command
info() {
  printf "\033[2 q"
  command info --vi-keys "$@"
}

if ! isAndroid; then
  sourceIf /usr/share/zsh/plugins/zsh-autosuggestions/zsh-autosuggestions.zsh
fi

# FIXME: I'm not exactly sure how to do this, specially because it messes up if I'm trying to use more than one X server.
if ! isAndroid; then
  gpg-connect-agent updatestartuptty /bye >/dev/null
fi

if [ "$_STARTUP_DEBUG_TIME" ] && [ -z "$_startup_time_shown" ]; then
  local current_time=$(date +"%s%3N")
  local time_elapsed=$(bc <<<"$current_time - $_STARTUP_DEBUG_TIME")
  printf >&2 "Time elapsed since startup: %s ms\n" "$time_elapsed"

  _startup_time_shown=1
fi

