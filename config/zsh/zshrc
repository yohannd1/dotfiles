#!/usr/bin/env zsh

# time debugging
if [ -z "$_STARTUP_DEBUG_TIME" ]; then
  _STARTUP_DEBUG_TIME=$(date +"%s%3N")
fi

ZSH_RC_PATH="${ZDOTDIR:-$HOME}/.zshrc"
XDG_CACHE_HOME=${XDG_CACHE_HOME:-$HOME/.cache}
XDG_CONFIG_HOME=${XDG_CONFIG_HOME:-$HOME/.config}

exists() { command -v "$1" &>/dev/null; }
setTitle() { printf '\033]0;%s\007' "$1"; }
isAndroid() { test "$(uname -o)" = Android; }
isRoot() { [ "$EUID" = 0 ] }

sourceIf() {
  [ $# != 1 ] && return 1
  [ -f "$1" ] && source "$1"
}

sourceOneOf() {
  for f in "$@"; do
    [ -r "$f" ] && { source "$f"; return 0; }
  done

  return 1
}

eval "$(lua "$DOTFILES/lib/z.lua" --init zsh enhanced once echo fzf)" \
  || printf >&2 "error: failed to load z.lua\n"

# Load some other libraries
sourceIf "$XDG_CONFIG_HOME/dots/env.sh"
sourceIf ~/storage/local/config/zshrc.personal

# general options
setopt interactivecomments # can use '#' for comments in interactive
setopt histignorespace # entries that start with a ' ' don't get to the history file
setopt PROMPT_SUBST # $(cmd) literals are run on $PS1/$PS2/$RPROMPT

# history config
HISTFILE="$XDG_CACHE_HOME/zsh-history"
HISTSIZE=SAVEHIST=100000
setopt sharehistory
setopt extendedhistory
setopt nobanghist # i fuc king hate !!!!!!!
setopt HIST_IGNORE_ALL_DUPS

# color plugin config
autoload -U colors && colors

# "edit in editor" plugin config
autoload -z edit-command-line
zle -N edit-command-line

# completion plugin config
autoload -U compinit # completion support
zstyle ':completion:*' menu select
zmodload zsh/complist
compinit
_comp_options+=(globdots) # include hidden files in autocomplete

# configure prompt
{
  precmd() {
    local cwdAbbreviated
    cwdAbbreviated=$(pwd | awk -v patt="^$HOME" '{ gsub(patt, "~"); print }')

    # set terminal title
    setTitle "$(whoami)@${HOST}:$cwdAbbreviated"
  }

  _ps1Symbol="%F{13}Î» "
  _ps1ExitCodeIndicator="%(?.. %F{4}exit:%F{10}%?)"
  _ps1JobCountIndicator="%(1j. %F{4}jobs:%F{10}%j.)"

  _ps1UserIndicator="%F{10}%n@%m "
  _ps1PathIndicator="%F{12}%~"
  _ps1MainBox="%F{3}[${_ps1PathIndicator}%F{3}]"

  if isAndroid; then
    _ps1Home="%F{13}... "
  else
    _ps1Home='%F{13}%n%F{4}@%F{13}%m '
  fi

  if [ "$SHELL_STACK" ]; then
    _quotedShellStack=$(printf "%q" "$SHELL_STACK")
    _ps1ShellStackIndicator=" %F{4}stack:%F{9}$_quotedShellStack"
  else
    _ps1ShellStackIndicator=""
  fi

  PS1="
${_ps1MainBox}${_ps1ExitCodeIndicator}${_ps1JobCountIndicator}${_ps1ShellStackIndicator}
$_ps1Symbol%b%F{5}"

  _getGitBranch() {
    git symbolic-ref --short -q HEAD 2>/dev/null
  }

  PS2="%F{13}| %{$reset_color%}"
  # ! isAndroid && RPROMPT='%F{11}${SHELL_STACK}'
  # ! isAndroid && RPROMPT='%F{11}$(_getGitBranch)'
  # [ "${NNNLVL:-0}" -ge 1 ] && PS1="%B%F{3}(%F{5}n%F{3})%b $PS1"
}

# use vim keys in tab completion menu
bindkey -M menuselect 'h' vi-backward-char
bindkey -M menuselect 'k' vi-up-line-or-history
bindkey -M menuselect 'l' vi-forward-char
bindkey -M menuselect 'j' vi-down-line-or-history
bindkey -v '^?' backward-delete-char

zshrc_updateRightPrompt() {
  isAndroid && return

  RPS1="${${KEYMAP/vicmd/[:N]}/(main|viins)}"
  RPS2=$RPS1
  zle reset-prompt
}

zle-keymap-select() {
  # change cursor shape for different vi modes
  if [ "$KEYMAP" = vicmd ] || [ "$1" = "block" ]; then
    printf '\033[1 q'
  elif [ "$KEYMAP" = main ] || [ "$KEYMAP" = viins ] \
         || [ "$KEYMAP" = '' ] || [ "$1" = 'beam' ]; then
    printf '\033[5 q'
  fi
  zshrc_updateRightPrompt
}
zle -N zle-keymap-select

zle-line-init() {
  zle -K viins # initiate `vi insert` as keymap
  # zshrc_updateRightPrompt # XXX: uncommenting this line glitches output when zsh-autosuggestions is not loaded... why??
}
zle -N zle-line-init

fzhistoryGen() {
  { [ -n "$ZSH_NAME" ] && fc -l 1 || history 1; } \
    | sed -r 's/ *[0-9]*\*? *//' \
    | tac | fzagnostic \
    | sed -r 's \\ \\\\ g'
}

fzhistory() {
  local result=$(fzhistoryGen) || return 1
  print -z "$result"
}

bindkey -s '^R' '\eS fzhistory\n'
[ "$FILEMAN" = "lf" ] && exists lf && bindkey -s '^o' '\eS lfAndCd\n'
[ "$FILEMAN" = "nnn" ] && exists nnn && bindkey -s '^o' '\eS nnnAndCd\n'

bindkey -s '^P' '\e[C'

bindkey -M vicmd 'Ã‡' edit-command-line

_fzselectGen() {
  fd | fzagnostic -m | tr '\n' '\0' | xargs -0 -I{} printf "%q" {}
}
_bind_fzselect() {
  LBUFFER="${LBUFFER}$(_fzselectGen)"
  local ret="$?"
  zle reset-prompt
  return "$ret"
}
bindkey '^F' _bind_fzselect
zle -N _bind_fzselect

bindkey -s '^[w' '\eS w\n'
bindkey -s '^[-' '\eS cd ..\n'

lfAndCd() {
  local tmp=$(mktemp)

  (cd "${1:-.}" && lf -last-dir-path="$tmp" "$@")

  if [ -f "$tmp" ]; then
    dir=$(cat "$tmp")
    rm -f "$tmp" >/dev/null
    [ -d "$dir" ] && [ "$dir" != "$(pwd)" ] && cd "$dir"
  fi

  return 0
}

nnnAndCd() {
  # block nesting of nnn in subshells
  if [ -n "$NNNLVL" ] && [ "${NNNLVL:-0}" -ge 1 ]; then
    printf "%s\n" "nnn is already running"
    return 1
  fi

  export NNN_TMPFILE="$XDG_CONFIG_HOME/nnn/.lastd"
  nnn "$@"
  if [ -f "$NNN_TMPFILE" ]; then
    . "$NNN_TMPFILE"
    rm -f "$NNN_TMPFILE" >/dev/null
  fi

  return 0
}

findFileGen() {
  printf "%s (%s)\n" "@updir" ".."
  printf "%s (%s)\n" "@here " "$(pwd)"

  local args=()
  [ "$FINDFILE_SHOWHIDDEN" ] && args+=('-H')
  [ "$FINDFILE_DIRONLY" ] && args+=('-td')

  fd "${args[@]}"
}

findFile() {
  local prog choice choices show_hidden

  if [ "$1" = "-H" ]; then
    show_hidden=1
    shift
  fi

  [ $# = 0 ] && prog="${EDITOR:-vi}" || prog="$1"
  choice="$(FINDFILE_SHOWHIDDEN="$show_hidden" findFileGen | fzagnostic)" || return 1

  case "$choice" in
    "@updir"*) cd .. && findFile ;; # FIXME: annoying recursion
    "@here "*) ;;
    *)
      if [ -f "$choice" ]; then
        "$prog" "$choice"
      elif [ -d "$choice" ]; then
        # FIXME: annoying recursion
        cd "$choice" && findFile
      else
        printf >&2 "Don't know what to do with choice %s; returning" "$choice"
        exit 1
      fi
      ;;
  esac
}

dup-term-gui() {
  [ "$WAYLAND_DISPLAY" ] || [ "$DISPLAY" ] || exit 2
  "$TERMINAL" &>/dev/null & disown
}
dup-term-tmux() {
  [ "$TMUX" ] || exit 2
  tmux new-window -c "$PWD"
}
dup-term-auto() {
  if [ "$WAYLAND_DISPLAY" ] || [ "$DISPLAY" ]; then
    dup-term-gui
  elif [ "$TMUX" ]; then
    dup-term-tmux
  else
    exit 2
  fi
}

alias Dgui='dup-term-gui'
alias Dt='dup-term-tmux'
alias Dtmux='dup-term-tmux'
alias D='dup-term-auto'

pushShell() {
  if [ "$SHELL_STACK" ]; then
    SHELL_STACK="$SHELL_STACK.${1:-zsh}" zsh
  else
    SHELL_STACK="${1:-zsh}" zsh
  fi
}

reloadRc() { source "$ZSH_RC_PATH" }
alias rl='reloadRc'

# Blocked commands
_warnBlockedCmd() {
  printf >&2 "[Command Blocked: %s]\n" "$1"
  return 1
}
r() { _warnBlockedCmd "$0"; }

warpToZ() { z -I -t . "$@"; }

alias w='warpToZ'
alias f='findFile'
alias fo='findFile open'
alias fof='findFile openfork'
alias fcd='FINDFILE_DIRONLY=1 findFile cd-to-file'

anyArgIs() {
  for arg in "${@:2}"; do
    [ "$arg" = "$1" ] && return 0 || true
  done
  return 1
}

## A wrapepr around `make` that attempts to find the file on
## parent directories if it's not on the current one.
make() {
  if anyArgIs "-f" "$@"; then
    printf >&2 'info: `-f` argument detected, running normal make\n'
    command make "$@"
  else
    dir=$(upfind 'Makefile') \
      || dir=$(upfind 'makefile') \
      || dir=$(upfind 'GNUmakefile') \
      || {
      printf >&2 "error: could not find any valid makefile...\n"
      return 1
    }
    (cd "$dir" && command make "$@")
  fi
}

e() { "$EDITOR" "$@"; }
se() { EDITOR=$(which "$EDITOR") sudoedit "$@"; } # yeah, workaround

alias g='menu-git'
alias G='tig status'

alias p3='python3'
pip() { python3 -m pip "$@"; }

alias sy='dotsync'
alias of='openfork'

sg() {
  # WM=plasma start-xserver
  wl-start niri
}

if exists nvim; then
  alias vi='nvim' vim='nvim'
elif exists vim; then
  alias vi='vim'
fi

# A wrapper for the `info` command
info() {
  printf "\033[2 q"
  command info --vi-keys "$@"
}

sourceOneOf \
  "$ZSH_PLUGIN_PATH/zsh-autosuggestions/zsh-autosuggestions.zsh" \
  /usr/share/zsh/plugins/zsh-autosuggestions/zsh-autosuggestions.zsh

_ept_canonicalize_str() {
  [ $# = 1 ] || return 1

  case "$1" in
    pt|br) printf "pt-br" ;;
    jp) printf "japanese" ;;
    .|en|es|fr) printf "%s" "$1" ;;
    it) printf "italian" ;;
    lt) printf "latin" ;;
    ru) printf "russian" ;;
    _*) printf "%s" "${1#_}" ;;
    *)
      printf >&2 "Unknown language: %s\n" "$1"
      return 1
      ;;
  esac
}

epic-translate() {
  local src dest text

  if test $# -lt 3; then
    printf >&2 "Usage: %s <SOURCE-LANG> <TARGET-LANG> <TEXT...>\n" "$(basename "$0")"
    return 1
  fi

  src=$(_ept_canonicalize_str "$1") || return $?
  shift

  dest=$(_ept_canonicalize_str "$1") || return $?
  shift

  args=()
  [ "$src" != "." ] && args+=("-s" "$src")
  [ "$dest" != "." ] && args+=("-t" "$dest")

  trans "${args[@]}" "$*"
}

T() { epic-translate "$@" 2>&1 | less; }
Td() { epic-translate "$1" "$1" "$2" 2>&1 | less; }

.r() { . "$DOTFILES/config/dots/env.sh" && dotf.common.res-reload; }
.re() { .r && dotf.wrap.terminal "$EDITOR"; }

# FIXME: I'm not exactly sure how to do this, specially because it messes up if I'm trying to use more than one X server.
if ! isAndroid; then
  gpg-connect-agent updatestartuptty /bye >/dev/null
fi

get-network-info() {
  printf >&2 "IPs:\n"
  ifconfig 2>/dev/null | grep inet | awk '{ print "  " $2 }'
  printf >&2 "User: %s\n" "$(whoami)"
  printf >&2 "Public IP: "
  curl ifconfig.me
  printf >&2 "\n"
}
alias N='get-network-info'

if [ "$_STARTUP_DEBUG_TIME" ] && [ -z "$_startup_time_shown" ]; then
  local current_time=$(date +"%s%3N")
  local time_elapsed=$(bc <<<"$current_time - $_STARTUP_DEBUG_TIME")
  printf >&2 "Time elapsed since startup: %s ms " "$time_elapsed"

  _startup_time_shown=1
fi

xampp-start() { sudo xampp start; }
xampp-stop() { sudo xampp stop; }
xampp-restart() { xampp-stop; xampp-start; }

# file related
ls() { command ls --color=auto "$@"; }
l() { command ls -l "$@"; }
la() { command ls -A "$@"; }
du() { command du -shc "$@"; }
sdu() { command du -shc "$@" | sort -h; }

# music related
ytdl() {
  exists yt-dlp && { yt-dlp "$@"; return "$?"; }
  exists youtube-dl && { youtube-dl "$@"; return "$?"; }
  return 1
}
alias Mq='mus queue'
alias Ma='mus add'
alias Ms='mus shuffle'
alias Mp='mus play'
alias Mc='mus clear'
_mm_showQueue() {
  {
    echo '(( Start ))'
    mus queue
    echo '(( End ))'
  }
}
mus-menu() {
  while true; do
    clear
    _mm_showQueue | grep -C 3 '^\*'
    printf >&2 "(Q)uit, (V)iew queue, (N)ext, (P)rev, (S)huffle\n"
    printf >&2 "[q/v/n/p/s] "
    read -k1 key
    printf >&2 "\n"
    case "$key" in
      q|Q) return 0 ;;
      v|V)
        _mm_showQueue | less
        ;;
      n|N) mus next ;;
      p|P) mus prev ;;
      s|S) mus shuffle ;;
      *) printf >&2 "Unknown command...\n" ;;
    esac
  done
}
alias M='mus-menu'

findTodos() { rg '\b(TODO|FIXME|XXX)\b'; }

gS() { (git add . && git commit); git pull && git push; }

# manual clipboard sync - this is depressing
clipX2W() { xclip -o -sel clip | wl-copy; }
clipW2X() { wl-paste | xclip -sel clip; }

# proc list searches - might be the fastest & simplest way to do this
procList() { ps -eo pid,args | tail +2 | sed 's/^ \+//g'; }
procSearch() {
  case $# in
    0) procList | fzagnostic -m ;;
    1) procList | rg "$1" ;;
    *) return 2 ;;
  esac
}
alias P='procSearch'

procDo() { procSearch | cut -d' ' -f1 | xargs -I{} "$@"; }

eachLine() {
  tr '\n' '\0' | xargs -0 -I{} "$@"
}

findMap() {
  [ $# -lt 2 ] && return 2
  fd "$1" | tr '\n' '\0' | xargs -0 -I{} "${@:2}"
}

tbw() {
  printf >&2 "NVME:\n"
  sudo -n nvme smart-log /dev/nvme0 | grep 'Data Units Written'
}

venv-python() {
  local _path="$(upfind venv)" || return 1
  "$_path/venv/bin/python" "$@"
}
alias vp='venv-python'

_fm_run() {
  "$@"
  local ec=$?
  if [ "$ec" != 0 ]; then
    printf >&2 "(exit code %d)" "$ec"
    read -k1
  fi
}

_fm_genDirList() {
  echo ".."
  fd -td -d1
}

fm() {
  ls -1

  while true; do
    printf >&2 "?> "
    read -k1 char
    case "$char" in
      q||) return ;;
      $) _fm_run "$SHELL" ;;
      n|) _fm_run nnnAndCd ;;
      ) clear ;;
      w) w ;;
      c)
        printf >&2 "\n"
        choice=$(_fm_genDirList | fzagnostic) \
          && cd "$choice" \
          && clear \
          && ls -1
        ;;
      *)
        printf >&2 "???\n"
        ;;
    esac
  done
}

cb() { nvim +"W3m $*"; }
cb-search() {
  local query=$(printf "%s" "$*" | tr ' ' '+')
  cb "https://lite.duckduckgo.com/lite/?q=$query"
}

# live grep
lg() { nvim +'Telescope live_grep'; }

fpga-ci9p() {
  case "$1" in
    detect)
      openFPGALoader -c ch347_jtag --detect
      ;;

    upload)
      openFPGALoader -c ch347_jtag -b colorlight-i9+ "$2"
      ;;

    fzupload)
      local choice
      choice=$(fd -HI '\.bit$' | fzagnostic) || return 1
      fpga-ci9p upload "$choice"
      ;;

    *) return 2 ;;
  esac
}

# World's laziest dict
dict() {
  local url="https://en.wiktionary.org/wiki/$1"
  w3m -dump "$url" | "$EDITOR"
}
