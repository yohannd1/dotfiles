#!/usr/bin/env janet

(use dotf-path)

(defn .vhdl-arch [arch entity]
  (printf "architecture %s of %s is" arch entity)
  (printf "begin")
  (printf "end architecture;"))

(defn .vhdl-clock-process [clk-in]
  (printf "process (%s)" clk-in)
  (printf "begin")
  (printf "  if rising_edge(%s) then" clk-in)
  (printf "    -- CODE HERE")
  (printf "  end if;")
  (printf "end process;"))

(defn .osc-fur [start labels]
  "Generate corrscope channel entries for furnace channels."
  (for i 0 (length labels)
    (def lb (in labels i))
    (unless (nil? lb)
      (printf "- !ChannelConfig")
      (printf "  wav_path: c_c%02d.wav" (+ start i))
      (printf "  label: %s" lb))))

(def- osc-lmms-peg
  (peg/compile
    '(* :d+ "_" (<- (any (if-not ".wav" 1))) ".wav")))

(defn .osc-lmms [&opt path]
  "Generate corrscope channel entries for LMMS tracks."


  (default path ".")

  (defn try-parse [x]
    (when (def m (peg/match osc-lmms-peg x))
      [x ;m]))

  (as->
    (os/dir path) .x
    (map try-parse .x)
    (filter |(not (nil? $)) .x)
    (each [base name] .x
      (printf "- !ChannelConfig")
      (printf "  wav_path: %s" (path/join path base))
      (printf "  label: %s" name))))

(defn ? []
  "Aux function for listing known snippets."
  (as->
    (all-bindings) .x
    (filter |(= (string/slice $ 0 1) ".") .x)
    (each x .x (pp x))))

(as->
  (:read stdin :all) .x
  (eval-string .x)
  (unless (nil? .x)
    (pp .x)))
