#!/usr/bin/env janet
# vim: foldmethod=marker foldenable foldmarker=<<<,>>>

# Preparations <<<

(defn dev-null []
  (assert (file/open "/dev/null" :r)))

(var *open-gui* (-> (os/getenv "OPEN_GUI" "") (length) (> 0)))
(var *open-new-window* (-> (os/getenv "OPEN_NEW_WINDOW" "") (length) (> 0)))

(defn get-prog-output [-args]
  (def (pipe-r pipe-w) (os/pipe))
  (def code (os/execute -args :p {:in (dev-null) :out pipe-w}))
  (when (= code 0)
    (-> pipe-r (:read math/int32-max) (string/trim))))

(defn state/init [file-]
  (var file file-)

  (def peg-url-protocol
    '{:a-z (choice (range "az") (range "AZ"))
      :main (* (capture (some :a-z)) "://")})

  (def peg-file-ext
    '{:main (* (? "./") :non-dot (some :extension))
      :non-dot (some (if-not "." 1))
      :extension (* "." (<- :non-dot))
      })

  (def open-alt (os/getenv "OPEN_ALT" "xdg-open"))

  (var protocol
    (-?> (peg/match peg-url-protocol file) (in 0)))

  (when (= protocol "file")
    (set protocol nil)
    (set file (string/slice file (length "file://"))))

  (def mime
    (let [out (get-prog-output ["file" "--dereference" "--brief" "--mime-type" "--" file])]
      # TODO: proper error handling (but why the hell doesn't it return 1 when it errors??)
      (unless (or (nil? out) (string/has-prefix? "cannot" out))
        out)))

  (def ext
    (if (nil? protocol)
      (-?> (peg/match peg-file-ext file) (last) (string/ascii-lower))
      nil))

  @{:open-alt open-alt
    :file file
    :mime mime
    :ext ext
    :protocol protocol
    })

(defn state/try
  "Try to open.
  `-type` can be
    :gui to block stdin and redirect stderr, useful for GUI apps;
    :tty to directly open the program in the TTY, if possible;
    :pop to run as TTY if possible, but opening it via `termup` if not;
    :fallback to open the alt program specified by OPEN_ALT;
    :block to not run the file and exit immediately"
  [self -type &opt -args]

  (def {:open-alt open-alt
        :file file
        :mime mime
        :protocol protocol
        :ext ext} self)

  (defn prog-exists [progn]
    (def args ["which" progn])
    (def pipes {:out (dev-null) :err (dev-null)})
    (-> (os/execute args :p pipes) (= 0)))

  (defn condit-open [prog]
    (when (prog-exists (-args 0))
      (if *open-gui*
        (os/exit (os/execute [;prog ;-args] :p {:in (dev-null) :err stdout}))
        (os/exit (os/execute -args :p)))))

  (match -type
    :gui (when (and *open-gui* (prog-exists (-args 0)))
           (os/exit (os/execute -args :p {:in (dev-null) :err stdout})))
    :tty (when (and (not *open-gui*) (prog-exists (-args 0)))
           (os/exit (os/execute -args :p)))
    :pop (condit-open ["termup" "-f"])
    :runpage (condit-open ["termup" "-f" "runpage"])
    :fallback (os/exit (os/execute [open-alt file] :p))
    :xdg (do
           (os/setenv "VIA_OPEN" "1")
           (os/exit (os/execute ["xdg-open" file])))
    :block (let [msg "Attempted to open blocked file (%s)\nMIME: %s; EXT: %s\n"]
             (:write stderr (string/format
                              msg file
                              (if (nil? mime) "nil" mime)
                              (if (nil? ext) "nil" ext)))
             (os/exit 1))
    _ (error (string/format "Unknown action for symbol %s" -type))))

(defn warn [msg]
  (if *open-gui*
    (os/execute ["notify-send" "--" msg] :px)
    (do
      (:write stderr msg)
      (:write stderr "\n"))))

(defn state/match
  "TODO: doc"
  [self mtype marg]

  (def {:mime mime
        :ext ext
        :protocol protocol} self)

  (defn go-through [list src-str]
    (unless (nil? src-str)
      (var ret false)
      (each elem list
        (cond
          (string? elem)
          (when (= src-str elem)
            (set ret true)
            (break))

          (tuple? elem)
          (match elem
            [:begins str] (when (string/has-prefix? str src-str)
                            (set ret true)
                            (break))
            [:ends str] (when (string/has-suffix? str src-str)
                          (set ret true)
                          (break))
            _ (error (string "TODO: error message lol")))

          (error (string "Expected either tuple or string, got " elem))
          ))
      ret))

  (match mtype
    :mime (go-through marg mime)
    :ext (go-through marg ext)
    :protocol (go-through marg protocol)
    _ (error (string/format "Unknown action for symbol %s" mtype))))
# >>>

(defn main [_ & args]
  (when (nil? (os/getenv "WAYLAND_DISPLAY"))
    (set *open-gui* true)
    (os/setenv "OPEN_GUI" "1"))

  (def help-str
    (string
      "Usage: open { <FILE> | <URL> }\n"
      "Environment variables:\n"
      "  OPEN_GUI: forces to open only apps in GUI mode.\n"
      "  OPEN_ALT: a fallback opener; default is xdg-open\n"))

  (when (-> args (length) (not= 1))
    (:write stderr help-str)
    (os/exit 1))

  (def s (state/init (in args 0)))
  (def {:file file} s)

  (defn t [-type &opt -arg] (state/try s -type -arg))
  (defn m [-type -arg] (state/match s -type -arg))
  (defn mt [mtype marg ttype &opt targ]
    (when (m mtype marg) (t ttype targ)))

  (def FILEMAN (os/getenv "FILEMAN"))
  (def BROWSER (os/getenv "BROWSER"))
  (def TERMBROWSER (os/getenv "TERMBROWSER"))
  (def EDITOR (os/getenv "EDITOR"))

  # (mt :mime [[:begins "application/"]] :block)

  (mt :mime ["inode/directory"] :pop [FILEMAN file])
  (mt :mime ["text/troff"] :runpage ["man" "-l" file])
  (when (or (m :mime ["inode/x-empty" [:ends "/xml"]])
            (m :ext ["txt" "log"]))
    (t :runpage ["cat" file]))

  (defn try-browser []
    (def args @[])
    (when (and *open-new-window*
               (or (= BROWSER "librewolf")
                   (= BROWSER "firefox")))
      (array/push args "--new-window"))
    (t :gui [BROWSER ;args file]))

  # urls
  (mt :protocol ["vscode" "steam"] :xdg)
  (when (m :protocol ["http" "https"])
    (try-browser)
    (t :tty [TERMBROWSER file]))

  # midi
  (when (m :ext ["mid" "midi"])
    (t :pop ["play-midi" file])
    (t :pop ["mpv" "--no-video" file]))

  # project files
  (mt :ext ["drawio"] :gui ["drawio" file])
  # (mt :ext ["godot"] :gui ["godot" "-e" file]) # FIXME: not working bruh
  (mt :ext ["yyp"] :gui ["GameMaker" file])

  # audio files
  (when (or (m :mime [[:begins "audio/"]])
            (m :ext ["aac" "flac" "m4a" "mpa" "mp2" "mp3"
                     "ogg" "wav" "wma"]))
    (t :pop ["mpv" "--no-video" file])
    (t :pop ["mocplay" file "opener"])
    (t :tty ["exiftool" file])
    (t :runpage ["mediainfo" file]))

  # music projects
  (mt :ext ["kdenlive"] :gui ["kdenlive" file])
  (mt :ext ["sunvox"] :gui ["sunvox" file])
  (when (m :ext ["ardour"])
    (t :gui ["ardour8" file])
    (t :gui ["ardour7" file])
    (t :gui ["ardour6" file])
    )
  (when (m :ext ["mmp" "mmpz"])
    (t :gui ["@lmms" file]) # a wrapper i have on my pc
    (t :gui ["lmms" file]))
  (mt :ext ["rad"] :gui ["radium" file])

  # image
  (when (m :mime ["image/vnd.djvu"])
    (t :runpage ["djvutxt" file])
    (t :runpage ["exiftool" file]))
  (when (or (m :mime [[:begins "image/"]])
            (m :ext ["gif" "jpg" "png" "webp"]))
    (t :gui ["sxiv" "-p" file])
    (t :runpage ["viu" "-n" file])
    (t :runpage ["img2txt" "--gamma=0.6" "--" file])
    (t :runpage ["exiftool" file]))

  # music modules
  (mt :ext ["mod" "it" "s3m" "mptm"] :pop ["openmpt123" file])
  (mt :ext ["nsf"] :pop ["nosefart" file])
  (mt :ext ["fur" "dmf"] :gui ["furnace" file])
  (mt :ext ["ptcop"] :gui ["@ptcollab" file])
  (mt :ext ["btm"] :gui ["BambooTracker" file])
  (mt :ext ["vgm" "vgz"] :pop ["vgmplay" file])

  (mt :ext ["swf"] :gui ["flashplayer" file])

  (def is-pdf (or (m :ext ["pdf"])
                  (m :mime ["application/pdf"])))

  (when (or is-pdf (m :ext ["djvu" "ps" "epub" "pdf"]))
    (t :gui ["zathura" file])
    (t :gui ["okular" file])
    (t :gui ["mupdf" file])
    )

  (when is-pdf
    (t :runpage ["pdftotext" "-l" "10" "-nopgbrk" "-q" "--" file "-"])
    (t :runpage ["mutool" "draw" "-F" "txt" "-i" "--" file "1-10"])
    (t :runpage ["exiftool" file]))

  (when (or (m :mime [[:begins "video/"]])
            (m :ext ["avi" "mkv" "mp4" "webm"]))
    (t :gui ["smplayer" file])
    (t :gui ["mpv" file])
    (t :tty ["mpv" "--no-video" file])
    (t :runpage ["mediainfo" file]))

  (mt :ext ["rar"] :runpage ["unrar" "l" "-p-" "--" file])
  (mt :ext ["7z"] :runpage ["7z" "l" "-p" "--" file])

  (when (m :ext ["a" "ace" "alz" "arc" "arj" "bz" "bz2" "cab" "cpio" "deb" "gz" "jar"
                 "lha" "lz" "lzh" "lzma" "lzo" "rpm" "rz" "t7z" "tar" "tbz2" "tgz"
                 "tlz" "txz" "tZ" "tzo" "war" "xpi" "xz" "Z" "zip"])
    (t :runpage ["atool" "--list" "--" file])
    (t :runpage ["bsdtar" "--list" "--file" file]))

  (when (m :ext ["torrent"])
    (t :pop ["rtorrent" file])
    (t :runpage ["transmission-show" "--" file]))

  (when (m :ext ["csv"])
    (t :pop ["csvlens" file])
    (t :gui ["libreoffice" "--nologo" "--calc" file]))

  (mt :ext ["odt" "rtf" "docx" "doc"]
      :gui ["@onlyoffice" file])

  (mt :ext
      [
       "odp" "pptx" "ppt" # presentation
       "ods" "xlsx" "xls" # spreadsheet
       "odg" # drawing
       ]
      :gui ["libreoffice" "--nologo" file])

  (mt :ext ["odt" "ods" "odp" "sxw"]
      :runpage ["odt2txt" file])

  (mt :ext ["kra"] :gui ["krita" file])
  (mt :ext ["pxo"] :gui ["pixelorama" file])
  (mt :ext ["blend"] :gui ["@blender" file])
  (mt :ext ["tic"] :gui ["tic80" file])
  (mt :ext ["rpy"] :pop [EDITOR file])
  (mt :ext ["ipynb"] :gui ["codium" file]) # vscode is decent for notebooks

  (when (m :ext ["md"])
    # (t :runpage ["glowrapper" file])
    (t :gui ["md-preview" file]))

  (when (m :ext ["tex"])
    (t :pop ["rifle-run" "run" "tex" file]))

  (when (m :ext ["acr"])
    (t :pop ["rifle-run" "run" "acrylic" file]))

  (when (m :ext ["htm" "html" "xhtml" "mhtml"])
    (try-browser)
    (t :runpage ["w3m" "-dump" file])
    (t :runpage ["lynx" "-dump" "--" file])
    (t :runpage ["elinks" "-dump" file]))

  (when (m :ext ["json"])
    (t :runpage ["jq" "--color-output" "." file])
    (t :runpage ["python3" "-m" "json.tool" "--" file]))

  (mt :ext ["vcd"] :gui ["pulseview" "-c" file])

  (t :fallback))
