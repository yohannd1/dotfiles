#!/usr/bin/env janet

(use dotf-utils)

(defn get-date-string []
  (def {:year year
        :month month
        :month-day month-day
        :hours hours
        :minutes minutes}
    (os/date nil true))

  (string/format
    "%04d-%02d-%02d %02d:%02d"
    year (inc month) (inc month-day) hours minutes))

(defn watch-date [data-chan]
  (forever
    (ev/give data-chan [:date (get-date-string)])
    (ev/sleep 2)))

(defn get-volume-string []
  (-> ["dwmblocks-volume"]
      (run<stdout)
      (assert "failed to get volume")))

(defn watch-volume [data-chan]
  (def notify-chan (ev/chan 1))

  (defn sink-line? [line]
    (truthy? (string/find "sink" line)))

  (defn handle-out [fd]
    (each line (stream-lines fd)
      (when (sink-line? line)
        (ev/give notify-chan 1))))

  (ev/go |(run< ["pactl" "subscribe"] {:out handle-out}))

  (ev/give notify-chan 1)
  (while (def _ (ev/take notify-chan))
    (ev/give data-chan [:volume (get-volume-string)])))

(defn main [& args]
  (def data-chan (ev/chan 1))
  (var old-string nil)

  # start the blocks
  (ev/go |(watch-volume data-chan))
  (ev/go |(watch-date data-chan))

  (ev/spawn
    (def outputs @{})
    (def names [:volume :date])

    (forever
      (match (ev/take data-chan)
        [name content] (set (outputs name) content)
        _ (error "unexpected..."))

      (def new-string
        (as->
          names .x
          (map |(-> outputs (in $) (or "???")) .x)
          (string/join .x " | ")))

      (when (not= new-string old-string)
        (:write stdout new-string)
        (:write stdout "\n")
        (:flush stdout)
        (set old-string new-string)))))
